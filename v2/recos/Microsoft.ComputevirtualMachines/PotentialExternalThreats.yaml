guid: 70b1d2be-e6c4-b54e-9959-b1b690f9e485
name: PotentialExternalThreats
title: Network access to the VM disk should be set to Disable public access and enable
  private access
description: Recommended changing to "Disable public access and enable private access"
  and creating a Private Endpoint to improve security by restricting direct public
  access and ensuring connections are made privately, enhancing data protection and
  minimizing potential external threats.
source:
  type: aprl
  file: azure-resources/Compute/virtualMachines/recommendations.yaml
  timestamp: July 24, 2024
services:
- Microsoft.Compute/virtualMachines
resourceTypes:
- Microsoft.Compute/virtualMachines
severity: 2
labels:
  area: Security
links: []
queries:
  arg: |-
    // Azure Resource Graph Query
    // Find all Disks with "Enable public access from all networks" enabled
    resources
    | where type =~ 'Microsoft.Compute/disks'
    | where properties.publicNetworkAccess == "Enabled"
    | project id, name, tags, lowerCaseDiskId = tolower(id)
    | join kind = leftouter (
        resources
        | where type =~ 'Microsoft.Compute/virtualMachines'
        | project osDiskVmName = name, lowerCaseOsDiskId = tolower(properties.storageProfile.osDisk.managedDisk.id)
        | join kind = fullouter (
            resources
            | where type =~ 'Microsoft.Compute/virtualMachines'
            | mv-expand dataDisks = properties.storageProfile.dataDisks
            | project dataDiskVmName = name, lowerCaseDataDiskId = tolower(dataDisks.managedDisk.id)
            )
            on $left.lowerCaseOsDiskId == $right.lowerCaseDataDiskId
        | project lowerCaseDiskId = coalesce(lowerCaseOsDiskId, lowerCaseDataDiskId), vmName = coalesce(osDiskVmName, dataDiskVmName)
        )
        on lowerCaseDiskId
    | summarize vmNames = make_set(vmName) by name, id, tostring(tags)
    | extend param1 = iif(isempty(vmNames[0]), "VMName: n/a", strcat("VMName: ", strcat_array(vmNames, ", ")))
    | project recommendationId = "70b1d2be-e6c4-b54e-9959-b1b690f9e485", name, id, tags, param1
    | order by id asc
