name: aprl-VariousAzureServicesAzurePolicies
title: Ensure that your VMs are compliant with Azure Policies
description: |-
  Keeping your virtual machine (VM) secure is crucial for the applications you run. This involves using various Azure services and features to ensure secure access to your VMs and the secure storage of your data, aiming for overall security of your VM and applications.
source:
  type: aprl
  file: azure-resources/Compute/virtualMachines/recommendations.yaml
  timestamp: July 24, 2024
resourceTypes:
- Microsoft.Compute/virtualMachines
severity: 2
labels:
  guid: c42343ae-2712-2843-a285-3437eb0b28a1
  area: Governance
links: []
queries:
  arg: |-
    // Azure Resource Graph Query
    // Find all VMs in "Non-compliant" state with Azure Policies
    policyresources
    | where type =~ "Microsoft.PolicyInsights/policyStates" and properties.resourceType =~ "Microsoft.Compute/virtualMachines" and properties.complianceState =~ "NonCompliant"
    | project
        policyDefinitionId = tolower(properties.policyDefinitionId),
        policyAssignmentId = tolower(properties.policyAssignmentId),
        targetResourceId = tolower(properties.resourceId)
    // Join the policy definition details
    | join kind = leftouter (
        policyresources
        | where type =~ "Microsoft.Authorization/policyDefinitions"
        | project policyDefinitionId = tolower(id), policyDefinitionDisplayName = properties.displayName
        )
        on policyDefinitionId
    | project policyDefinitionId, policyDefinitionDisplayName, policyAssignmentId, targetResourceId
    // Join the policy assignment details
    | join kind = leftouter (
        policyresources
        | where type =~ "Microsoft.Authorization/policyAssignments"
        | project policyAssignmentId = tolower(id), policyAssignmentDisplayName = properties.displayName
        )
        on policyAssignmentId
    | project policyDefinitionId, policyDefinitionDisplayName, policyAssignmentId, policyAssignmentDisplayName, targetResourceId
    // Join the target resource details
    | join kind = leftouter (
        resources
        | where type =~ "Microsoft.Compute/virtualMachines"
        | project targetResourceId = tolower(id), targetResourceIdPreservedCase = id, targetResourceName = name, targetResourceTags = tags
        )
        on targetResourceId
    | project
        recommendationId = "c42343ae-2712-2843-a285-3437eb0b28a1",
        name = targetResourceName,
        id = targetResourceIdPreservedCase,
        tags = targetResourceTags,
        param1 = strcat("DefinitionName: ", policyDefinitionDisplayName),
        param2 = strcat("DefinitionID: ", policyDefinitionId),
        param3 = strcat("AssignmentName: ", policyAssignmentDisplayName),
        param4 = strcat("AssignmentID: ", policyAssignmentId)
