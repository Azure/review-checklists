guid: 3c8fa7c6-6b78-a24a-a63f-348a7c71acb9
name: OutboundSnatPortUsage
title: Monitor Azure Firewall metrics
description: Monitor Azure Firewall for overall health, processed throughput, and
  outbound SNAT port usage. Get alerted before limits impact services. Consider NAT
  gateway integration with zonal deployments; note limitations with zone redundant
  firewalls and secure virtual hub networks.
source:
  type: aprl
  file: azure-resources/Network/azureFirewalls/recommendations.yaml
  timestamp: July 24, 2024
services:
- Microsoft.Network/azureFirewalls
resourceTypes:
- Microsoft.Network/azureFirewalls
severity: 0
labels:
  area: Monitoring and Alerting
links: []
queries:
  arg: |-
    // Azure Resource Graph Query
    // List all Azure Firewalls resources in-scope, along with any metrics associated to Azure Monitor alert rules, that are not fully configured.
    resources
    | where type == "microsoft.network/azurefirewalls"
    | project firewallId = tolower(id), name, tags
    | join kind = leftouter (
        resources
        | where type == "microsoft.insights/metricalerts"
        | mv-expand properties.scopes
        | mv-expand properties.criteria.allOf
        | where properties_scopes contains "azureFirewalls"
        | project metricId = tolower(properties_scopes), monitoredMetric = properties_criteria_allOf.metricName, tags
        | summarize monitoredMetrics = make_list(monitoredMetric) by tostring(metricId)
        | project
            metricId,
            monitoredMetrics,
            allAlertsConfigured = monitoredMetrics contains("FirewallHealth") and monitoredMetrics contains ("Throughput") and monitoredMetrics contains ("SNATPortUtilization")
    ) on $left.firewallId == $right.metricId
    | extend alertsNotFullyConfigured = isnull(allAlertsConfigured) or not(allAlertsConfigured)
    | where alertsNotFullyConfigured
    | project recommendationId = "c8fa7c6-6b78-a24a-a63f-348a7c71acb9", name, id = firewallId, tags, param1 = strcat("MetricsAlerts:", monitoredMetrics)
