guid: 8bb4a57b-55e4-d24e-9c19-2679d8bc779f
name: NetworkSecurityGroupRules
title: Monitor changes in Network Security Groups with Azure Monitor
description: Create Alerts with Azure Monitor for operations like creating or updating
  Network Security Group rules to catch unauthorized/undesired changes to resources
  and spot attempts to bypass firewalls or access resources from the outside.
source:
  type: aprl
  file: azure-resources/Network/networkSecurityGroups/recommendations.yaml
  timestamp: July 24, 2024
services:
- Microsoft.Network/networkSecurityGroups
resourceTypes:
- Microsoft.Network/networkSecurityGroups
severity: 2
labels:
  area: Monitoring and Alerting
links: []
queries:
  arg: |-
    // Azure Resource Graph Query
    // Find all Network Security Groups without alerts for modification configured.
    resources
    | where type =~ "Microsoft.Network/networkSecurityGroups"
    | project name, id, tags, lowerCaseNsgId = tolower(id)
    | join kind = leftouter (
        resources
        | where type =~ "Microsoft.Insights/activityLogAlerts" and properties.enabled == true
        | mv-expand scope = properties.scopes
        | where scope has "Microsoft.Network/networkSecurityGroups"
        | project alertName = name, conditionJson = dynamic_to_json(properties.condition.allOf), scope
        | where conditionJson has '"Administrative"' and (
            // Create or Update Network Security Group
            (conditionJson has '"Microsoft.Network/networkSecurityGroups/write"') or
            // All administrative operations
            (conditionJson !has '"Microsoft.Network/networkSecurityGroups/write"' and conditionJson !has '"Microsoft.Network/networkSecurityGroups/delete"' and conditionJson !has '"Microsoft.Network/networkSecurityGroups/join/action"')
            )
        | project lowerCaseNsgIdOfScope = tolower(scope)
        )
        on $left.lowerCaseNsgId == $right.lowerCaseNsgIdOfScope
    | where isempty(lowerCaseNsgIdOfScope)
    | project recommendationId = "8bb4a57b-55e4-d24e-9c19-2679d8bc779f", name, id, tags, param1 = "ModificationAlert: Not configured/Disabled"
